/-
This file defines the constructive real numbers â„›,
defined by sequences of shrinking and dwindling rational segments
-/

import ..Intuitionism.segment

/--
We say that a sequence of segments is 'shrinking' if each segment is contained within its predecessor
-/
def shrinking (r : â„• â†’ ð•Š) := âˆ€ n, r (n + 1) âŠ‘ r n

/--
We say that a sequence of segments is 'dwindling' if we can make the segments arbitrarily small
-/
def dwindling (r : â„• â†’ ð•Š) := âˆ€ q : â„š, q > 0 â†’ âˆƒ n : â„•, (r n).snd - (r n).fst < q

/--
The definition of real sequences â„›, representing the real numbers
(Called 'real_seq' here to not interfere with the classical real numbers,  
which are already defined in Lean using Cauchy sequences)  
A real sequence is a sequence of rational segments that shrinks and dwindles
-/
def real_seq := {r : â„• â†’ ð•Š // shrinking r âˆ§ dwindling r}

notation `â„›` := real_seq

namespace real_seq

/--
Used to extract the underlying sequence of rational segments
-/
def seq (r : â„›) : â„• â†’ ð•Š := subtype.val r

-- We can turn a real segment into a sequence of rationals by only taking the first position
def fst (r : â„›): â„• â†’ â„š :=
begin
    intro n,
    exact (r.seq n).fst,
end

-- We can turn a real segment into a sequence of rationals by only taking the second position
def snd (r : â„›) : â„• â†’ â„š :=
begin
    intro n,
    exact (r.seq n).snd,
end

lemma shrinking (r : â„›) : shrinking r.val := (subtype.property r).elim_left

lemma dwindling (r : â„›) : dwindling r.val := (subtype.property r).elim_right

theorem contained_of_le (r : â„›) {n m : â„•} (hâ‚ : n â‰¤ m) : r.seq m âŠ‘ r.seq n :=
begin
    induction hâ‚ with d hd,
    {-- need to prove: seq r n âŠ‘ seq r n
        refl,
    },
    {-- need to prove: seq r (nat.succ d) âŠ‘ seq r n
        transitivity (seq r d),
        {-- need to prove: seq r (nat.succ d) âŠ‘ seq r d
            exact r.shrinking d,
        },
        {-- need to prove: seq r d âŠ‘ seq r n
            exact hâ‚_ih,
        }
    }
end

def lt (x y : â„›) : Prop := âˆƒ n : â„•, x.seq n < y.seq n

infix `<` := lt

def le (x y : â„›) : Prop := âˆ€ n : â„•, x.seq n â‰¤ y.seq n

infix `â‰¤` := le

def apart (x y : â„›) : Prop := x < y âˆ¨ y < x

infix `#` := apart

def eq (x y : â„›) : Prop := âˆ€ n : â„•, x.seq n â‰ˆ y.seq n

infix `='`:50 := eq

def ne (x y : â„›) : Prop := Â¬ x =' y

infix `â‰ '`:50 := ne

@[trans] theorem lt_trans (x y z: â„›) (hâ‚ : x < y) (hâ‚‚ : y < z) : x < z :=
begin
    cases hâ‚ with n hn,
    cases hâ‚‚ with m hm,
    use max m n,
    transitivity seq y (max m n),
    {-- need to prove: seq x (max m n) < seq y (max m n)
        have hâ‚ := contained_of_le x (le_max_right m n),
        have hâ‚‚ := contained_of_le y (le_max_right m n),
        apply lt_of_le_of_lt hâ‚.elim_right,
        apply lt_of_lt_of_le hn,
        exact hâ‚‚.elim_left,
    },
    {-- need to prove: seq y (max m n) < seq z (max m n)
        rw segment.lt at *,
        have hâ‚ := contained_of_le y (le_max_left m n),
        have hâ‚‚ := contained_of_le z (le_max_left m n),
        apply lt_of_le_of_lt hâ‚.elim_right,
        apply lt_of_lt_of_le hm,
        exact hâ‚‚.elim_left,
    }
end

lemma lt_or_lt_from_sub_lt_sub {a b c d : â„š} (h : a - b < c - d) : a < c âˆ¨ d < b :=
begin
    have hac := lt_trichotomy a c,
    cases hac with hl hr,
    {-- case: a < c
        left,
        exact hl,
    },
    {-- case: a = c âˆ¨ c < a
        right,
        cases hr with heq hlt,
        {-- case: a = c
            rwa [heq, sub_eq_add_neg, sub_eq_add_neg, add_lt_add_iff_left, neg_lt_neg_iff] at h,
        },
        {-- case: c < a
            rw â† sub_lt_zero,
            transitivity (c-a),
            {-- need to prove: d - b < c - a
                rwa [lt_sub_iff_add_lt',
                    â† add_sub_assoc,
                    sub_lt_iff_lt_add',
                    â† lt_sub_iff_add_lt,
                    add_sub_assoc, add_comm,
                    â† sub_lt_iff_lt_add] at h,
            },
            {-- need to prove: c - a < 0
                rw â† sub_lt_zero at hlt,
                exact hlt,
            }
        }
    }
end

theorem lt_cotrans (x y z : â„›) (hâ‚ : x < y) : x < z âˆ¨ z < y :=
begin
    cases hâ‚ with n hn,
    rwa [segment.lt, â† sub_pos, â† gt_from_lt] at hn,
    have hz := z.dwindling (segment.fst (seq y n) - segment.snd (seq x n)) hn,
    cases hz with m hm,
    have hmâ‚‚ := lt_or_lt_from_sub_lt_sub hm,
    cases hmâ‚‚ with zlty xltz,
    {-- case: z.snd m < y.fst n
        right,
        use max m n,
        have hâ‚ := contained_of_le z (le_max_left m n),
        have hâ‚‚ := contained_of_le y (le_max_right m n),
        apply lt_of_le_of_lt hâ‚.elim_right,
        apply lt_of_lt_of_le zlty,
        exact hâ‚‚.elim_left,
    },
    {-- case: x.snd n < z.fst m
        left,
        use max m n,
        have hâ‚ := contained_of_le z (le_max_left m n),
        have hâ‚‚ := contained_of_le x (le_max_right m n),
        apply lt_of_le_of_lt hâ‚‚.elim_right,
        apply lt_of_lt_of_le xltz,
        exact hâ‚.elim_left,
    }
end

theorem apart_cotrans (x y z : â„›) (h : x # y) : x # z âˆ¨ z # y :=
begin
    cases h with hâ‚ hâ‚‚,
    {-- case: x < y
        cases lt_cotrans x y z hâ‚ with hz hz,
        {-- case: x < z
            left,
            left,
            exact hz,
        },
        {-- case: z < y
            right,
            left,
            exact hz,
        }
    },
    {-- case: case y < x
        cases lt_cotrans y x z hâ‚‚ with hz hz,
        {-- case: y < z
            right,
            right,
            exact hz,
        },
        {-- case: z < x
            left,
            right,
            exact hz,
        }
    }
end

theorem le_iff_not_lt (x y : â„›) : x â‰¤ y â†” Â¬ y < x :=
begin
    split,
    {-- need to prove: x â‰¤ y â†’ Â¬ y < x
        intros hâ‚ hâ‚‚,
        cases hâ‚‚ with n hn,
        have hnâ‚ := hâ‚ n,
        rw segment.le_iff_not_lt at hnâ‚,
        exact hnâ‚ hn,
    },
    {-- need to prove: Â¬ y < x â†’ x â‰¤ y
        intros h n,
        rw segment.le_iff_not_lt,
        revert n,
        exact forall_not_of_not_exists h,
    }
end

@[trans] theorem le_trans (x y z : â„›) (hâ‚ : x â‰¤ y) (hâ‚‚ : y â‰¤ z) : x â‰¤ z :=
begin
    rw le_iff_not_lt at *,
    intro zltx,
    cases lt_cotrans z x y zltx with hz hz,
    {-- case: z < y
        exact hâ‚‚ hz,
    },
    {-- case: y < x
        exact hâ‚ hz,
    }
end

@[refl] theorem le_refl (x : â„›) : x â‰¤ x :=
begin
    intro n,
    refl,
end

theorem eq_iff_not_apart (x y : â„›) : x =' y â†” Â¬ x # y :=
begin
    split,
    {-- need to prove: x = y â†’ Â¬ x # y
        intros hâ‚ hâ‚‚,
        cases hâ‚‚ with xlty yltx,
        {-- case: x < y
            cases xlty with n hn,
            have hnâ‚ := hâ‚ n,
            rw segment.lt_iff_not_le at hn,
            exact hn hnâ‚.elim_right,
        },
        {-- case: y < x
            cases yltx with n hn,
            have hnâ‚ := hâ‚ n,
            rw segment.lt_iff_not_le at hn,
            exact hn hnâ‚.elim_left,
        }
    },
    {-- need to prove: Â¬ x # y â†’ x = y
        intros h n,
        rw apart at h,
        rw segment.touches,
        rw not_or_distrib at h,
        split,
        {-- need to prove: seq x n â‰¤ seq y n
            rw segment.le_iff_not_lt,
            have hâ‚ := forall_not_of_not_exists h.elim_right n,
            exact hâ‚,
        },
        {-- need to prove: seq y n â‰¤ seq x n
            rw segment.le_iff_not_lt,
            have hâ‚‚ := forall_not_of_not_exists h.elim_left n,
            exact hâ‚‚,
        }
    }
end

@[trans] theorem eq_trans (x y z: â„›) (hâ‚ : x =' y) (hâ‚‚ : y =' z) : x =' z :=
begin
    rw eq_iff_not_apart,
    intro hâ‚ƒ,
    have hâ‚„ := apart_cotrans x z y hâ‚ƒ,
    cases hâ‚„ with xay yaz,
    {-- case: x # y
        rw eq_iff_not_apart at hâ‚,
        exact hâ‚ xay,
    },
    {-- case: y # z
        rw eq_iff_not_apart at hâ‚‚,
        exact hâ‚‚ yaz,
    }
end

@[refl] theorem eq_refl (x : â„›) : x =' x :=
begin
    intro n,
    refl,
end

theorem le_stable (x y : â„›) : Â¬Â¬x â‰¤ y â†’  x â‰¤ y :=
begin
    rw le_iff_not_lt,
    exact not_of_not_not_not,
end

theorem eq_stable (x y : â„›) : Â¬Â¬x =' y â†’ x =' y :=
begin
    rw eq_iff_not_apart,
    exact not_of_not_not_not,
end

end real_seq


/-
TODO:
1) Define inclusion from rationals intro reals using: â„š â†’ â„• â†’ ð•Š := Î» q, Î» n, (q - 1 / 2^n, q + 1 / 2^n)
2) Define 0 using Î» n, (0, 0)
3) Show that 0 in (2) and the embedding of 0 in (1) are equal
4) Theorems in 3.4 of int2

Create other files for:
5) Intermediate Value Theorem and its constructive counterparts
6) Completeness of (â„›, â‰¤)
7) Every real function is continuous (Needs Fan Theorem)
-/